program all_vars_labeled, rclass
    version 12.1
    foreach v of varlist * {
        // Automatic labels generated by collapse don't count
        // using the variable name in a regular expression is a little
        //questionable, but stata blocks most unusual characters already.
        local lab : variable label `v'
        
        if "`lab'" == "" | regexm("`lab'", "^\((mean|median|sd|semean|sum|rawsum|count|max|min|iqr|first|last|firstnm|lastnm|)\) `v'") {
            local missing_varlist = "`missing_varlist' `v'"
        }
    }
    if "`missing_varlist'" == "" {
        local all_labeled = 1
    }
    else {
        local all_labeled = 0
        disp "missing labels for these variables: `missing_varlist'"
    }
    return local all_labeled `all_labeled'
end
